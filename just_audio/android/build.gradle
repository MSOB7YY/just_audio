group 'com.ryanheise.just_audio'
version '1.0'
def args = ["-Xlint:deprecation","-Xlint:unchecked","-Werror"]

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
          url = project(':just_audio').file('repo')
        }
    }
}

project.getTasks().withType(JavaCompile) {
    options.compilerArgs.addAll(args)
}

apply plugin: 'com.android.library'

android {
    sourceSets {
        main {
            java {
                srcDirs = ['src']
            }
        }
    }
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.ryanheise.just_audio'
    }
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "androidx.media3:media3-decode-ffmpeg:1.2.0" // ffmpeg extension
    def exoplayer_version = "1.4.0"
    implementation "androidx.media3:media3-exoplayer:$exoplayer_version"
    implementation "androidx.media3:media3-exoplayer-dash:$exoplayer_version"
    implementation "androidx.media3:media3-exoplayer-hls:$exoplayer_version"
    implementation "androidx.media3:media3-exoplayer-smoothstreaming:$exoplayer_version"
    implementation "androidx.annotation:annotation:1.8.1"
}
